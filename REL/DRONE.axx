	.file	"drone.asm"
	.title	"basketball drone code"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"mproc.equ"		;Mproc equates
	.include	"disp.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"audit.equ"
	.include	"shawn.hdr"		;Macros
	.asg		0,SEQT
	.include	"plyr.equ"





	.ref	plyrobj_t,plyrproc_t
	.ref	ballobj_p
	.ref	ballpnum
	.ref	seekdirdist_obxz128

	.ref	drone2on
	.ref	drone2_main,drone2_adjskill,drone2_setuptob

	.ref	game_time,gmqrtr
	.ref	shotimer

	.ref	team1,team2

	.ref	RNDPER
	.ref	PSTATUS
	.ref	GET_ADJ





	.bss	drnzzcnt	,16	;Drone zigzag mode cntdn
	.bss	drnzzmode	,16	;Drone zigzag mode (0-?)





	.text




 SUBR	drone_main

	move	@drone2on,b0
	jrnz	drone2_main


	move	@ballpnum,a14
	jrn	drone_chaseball		;No owner?

	move	*a13(plyr_ownball),a1
	jrz	drone_defence		;We don't have ball?
	jrn	drone_offwoball		;Teammate has ball?



	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrz	dbditmdrone		;Teammate is a drone?

	move	*a13(plyr_d_cflgs),a2
	btst	DRN_PASS_B,a2
	jrz	dbdinopass

	movi	BUT2_M<<8+BUT2_M,a14	;>Make him pass
	jruc	dbdidocmd

dbdinopass
	btst	DRN_SHOOT_B,a2
	jrz	dbdinoshoot
	movi	BUT1_M|BUT3_M,a14	;>Make him shoot
	movk	3,a0
	move	a0,*a13(plyr_d_seekcnt)
dbdidocmd
	move	*a11,a0
	sll	32-4,a0
	srl	32-4,a0
	ori	BUT3_M,a0		;+turbo
dbdinoturb
	or	a14,a0
	move	a0,*a11
	clr	a0
	move	a0,*a13(plyr_d_cflgs)
	jruc	dbdix

dbdinoshoot
dbditmdrone

	movk	1,a0
	move	a0,*a13(plyr_d_mode)


	move	*a13(plyr_seqflgs),a2
	btst	PASS_B,a2
	jrnz	dbdikilbuts

	move	*a13(plyr_jmpcnt),a14
	jrnz	dbdiinjmp

	move	*a11,a0
	btst	BUT1_B,a0
	jrnz	dbdifake			;Shoot button down?


	btst	SHOOT_B,a2
	jrnz	dbdiinjmp


	move	*a13(plyr_dribmode),a14
	jrn	dbdinodrib

	move	*a13(plyr_num),a14	;0CHhk for breakaway
	srl	1,a14
	movk	1,a0
	xor	a0,a14
	sll	6,a14			;*64
	addi	plyrproc_t,a14
	move	*a14+,a2,L
	move	*a14+,a3,L

	move	*a2(plyr_hpdist),a2
	move	*a3(plyr_hpdist),a3
	move	*a13(plyr_ohpdist),a4

	cmp	a4,a2
	jrlt	dbdishootrnd		;He's closer?
	cmp	a4,a3
	jrlt	dbdishootrnd		;He's closer?

	move	*a13(plyr_ohoopx),a0
	movi	CZMID,a1
	callr	drone_seekxy
	move	*a13(plyr_d_skill),a14
	cmpi	-11-3,a14
	jrle	dbdinoturb2
	ori	BUT3_M,a0		;Push turbo
	move	a0,*a11
dbdinoturb2

	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	dbdishoot2			;Less than 2 secs?
	move	@shotimer+16,a1		;Tens
	jrnz	dbdiscok
	move	@shotimer,a1		;Ones
	subk	2,a1
	jrlt	dbdishoot2			;Less than 2 secs?
dbdiscok
	move	*a13(plyr_ohpdist),a14
	cmpi	170,a14
	jrge	dbdix			;Too far?
	cmpi	50,a14
	jrlt	dbdishoot2			;Close?
	movk	01fH,a0
	callr	rnd
	jrnz	dbdix
	jruc	dbdishoot2


dbdinodrib					;0CaHn't dribble
	move	*a13(plyr_ohpdist),a0
	cmpi	230,a0
	jrlt	dbdishoot2
	callr	drone_pass
	jrnz	dbdix			;Pass OK?
	movk	01fH,a0
	callr	rnd
	jrnz	dbdix			;97%?
	jruc	dbdishoot2


dbdishootrnd
	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	dbdishoot2			;Less than 2 secs?
	move	@shotimer+16,a1
	jrnz	dbdiscok2
	move	@shotimer,a1
	subk	2,a1
	jrlt	dbdishoot2			;Less than 2 secs?
dbdiscok2
	PUSH	a6,a7
	move	*a13(plyr_ohoopx),a6
	movi	CZMID,a7
	calla	seekdirdist_obxz128
	PULL	a6,a7

	move	*a13(plyr_o1dist),a14
	cmpi	80,a14
	jrgt	dbdio1dok			;He's too far?
	cmp	a14,a1
	jrlt	dbdio1dok			;I'm closer?
	move	*a13(plyr_o1dir),a2
	sub	a0,a2
	abs	a2
	cmpi	040H,a2
	jrle	dbdio1dsml
	subi	080H,a2
	abs	a2
dbdio1dsml	subk	32,a2
	jrlt	dbdigoaround		;In front of me?
dbdio1dok
	move	*a13(plyr_o2dist),a14
	cmpi	80,a14
	jrgt	dbdio2dok			;He's too far?
	cmp	a14,a1
	jrlt	dbdio2dok			;I'm closer?
	move	*a13(plyr_o2dir),a2
	sub	a0,a2
	abs	a2
	cmpi	040H,a2
	jrle	dbdio2dsml
	subi	080H,a2
	abs	a2
dbdio2dsml	subk	32,a2
	jrlt	dbdigoaround		;In front of me?
dbdio2dok
	jruc	dbdirunath

dbdigoaround				;>Opponent in my way
	move	*a13(plyr_tmdist),a0
	cmpi	70,a0
	jrlt	dbdigoa			;Teammate too close?

	callr	drone_pass
	jrnz	dbdix			;Pass OK?

dbdigoa
	movi	drnzzcnt,a2
	move	*a2,a0
	subk	1,a0
	jrgt	dbdizzsame

	movk	1,a0			;New mode
	callr	rnd
	move	a0,*a2(drnzzmode-drnzzcnt)
	movi	TSEC-10,a0
	callr	rndrng0
	addk	10,a0
dbdizzsame
	move	a0,*a2

	move	*a13(plyr_ohoopx),a0
	movi	CZMID,a1
	callr	drone_seekxy
	jrz	dbdishoot2			;In position?
	sll	3,a0			;*8
	addi	dbdijbits_t,a0

	move	*a2(drnzzmode-drnzzcnt),a14
	jrz	dbdimd1
	addi	16*8,a0
dbdimd1	movb	*a0,a0
	move	a0,*a11

	cmpi	80,a4
	jrlt	dbdishoot2			;Close enough for jam?

	jruc	dbditryshot


dbdirunath					;>I have a clr path to hoop!
	move	*a13(plyr_o1dist),a14
	cmpi	65,a14
	jrlt	dbdigoa			;He's too close?
	move	*a13(plyr_o2dist),a14
	cmpi	65,a14
	jrlt	dbdigoa			;He's too close?

	move	*a13(plyr_ohoopx),a0
	movi	CZMID,a1
	callr	drone_seekxy
	move	*a13(plyr_d_skill),a14
	cmpi	-11-3,a14
	jrle	dbditryshot
	ori	BUT3_M,a0		;Turbo
	move	a0,*a11


dbditryshot
	cmpi	50,a4
	jrlt	dbdishoot2			;Close enough for jam?

	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	dbdix			;Teammate is a human?

	cmpi	230,a4			;0CHhk if we need 3ptrs
	jrlt	dbdino3
	cmpi	290,a4
	jrgt	dbdino3

	movk	6,a1
	move	@game_time,a2,L
	cmpi	01010000H,a2
	jrgt	dbdihvtime			;Enough time?
	movk	3,a1
dbdihvtime
	move	*a13(plyr_ptsdown),a14
	cmp	a1,a14
	jrlt	dbdino3

	cmpi	040000H,a2
	jrlt	dbdishoot2			;Less than 40 secs?

	movk	8,a0
	move	*a13(plyr_o1dist),a14
	cmpi	70,a14
	jrlt	dbdirndsht			;He's close?
	move	*a13(plyr_o2dist),a14
	cmpi	70,a14
	jrlt	dbdirndsht			;He's close?
	jruc	dbdishoot2

dbdino3
	movi	50,a0
	move	*a13(plyr_d_skill),a14
	cmpi	-8,a14
	jrgt	dbdirndsht
	movk	30,a0
dbdirndsht	callr	rndrng0
	move	a0,a0
	jrnz	dbdix

dbdishoot
	move	*a13(plyr_ohpdist),a14
	cmpi	240,a14
	jrge	dbdix			;Too far?

dbdishoot2	move	*a11,a0			;>Shoot
	ori	BUT1_M|BUT3_M,a0
	move	a0,*a11
	movk	1,a0			;Max fakes
	move	a0,*a13(plyr_d_seekcnt)
	jruc	dbdix



dbdifake					;>Jumping but still on gnd
	move	*a13(plyr_d_seekcnt),a2
	jrle	dbdix			;No fakes?
	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	dbdix			;Teammate is a human?
	movk	01fH,a0
	callr	rnd
	jrnz	dbdix

	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	dbdix			;Less than 2 secs?
	move	@shotimer+16,a1		;Tens
	jrnz	dbdifk
	move	@shotimer,a1		;Ones
	subk	2,a1
	jrlt	dbdix			;Less than 2 secs?
dbdifk
	subk	1,a2
	move	a2,*a13(plyr_d_seekcnt)
	jruc	dbdikilbuts


dbdiinjmp
	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrz	dbditmdrn			;Teammate is a drone?

	move	*a13(plyr_tmproc_p),a0,L
	move	*a0(PA11),a0,L
	move	*a0,a0			;Get teammates ctrl bits
	btst	BUT1_B,a0
	jrnz	dbdix			;Holding shoot button?
	jruc	dbdikilbuts
dbditmdrn
	move	*a3(plyr_seqflgs),a0
	btst	BLOCKREB_B,a0
	jrnz	dbdikilbuts		;Got a rebound?

	movk	15,a0
	callr	rndrng0
	move	a0,a0
	jrz	dbdikilbuts		;Cause shoot?

	movk	7,a0
	callr	rnd
	jrnz	dbdix			;88%?

	move	*a13(plyr_ptsdown),a14
	addk	5,a14
	jrlt	dbdipss			;Winning by 05H?
	move	*a13(plyr_seqflgs),a14
	btst	DUNK_B,a14
	jrnz	dbdix			;In a dunk?
	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	dbdix			;Less than 2 secs?

dbdipss	callr	drone_pass

	jruc	dbdix



dbdikilbuts
	clr	a0			;>Let go of shoot button
	move	a0,*a11


dbdix	rets


dbdijbits_t
	.byte	0,JOYL_M,JOYR_M,0
	.byte	JOYD_M,JOYD_M|JOYL_M,JOYD_M|JOYR_M,0
	.byte	JOYU_M,JOYU_M|JOYL_M,JOYU_M|JOYR_M,0
	.byte	0,0,0,0

	.byte	0,JOYR_M,JOYL_M,0
	.byte	JOYU_M,JOYU_M|JOYR_M,JOYU_M|JOYL_M,0
	.byte	JOYD_M,JOYD_M|JOYR_M,JOYD_M|JOYL_M,0
	.byte	0,0,0,0




 SUBRP	drone_offwoball

	clr	a0
	move	a0,*a13(plyr_d_cflgs)


	move	*a13(plyr_d_mode),a14
	jrgt	tuubinmd			;Already in mode?

	movk	1,a0
	move	a0,*a13(plyr_d_mode)

	callr	drone_getcurskillo
	addi	tuubmdsk_t,a0
	move	*a0,a0
	callr	rndrng0
	addk	1,a0
	move	a0,*a13(plyr_d_seekcnt)
tuubinmd


	move	*a13(plyr_dir),a3

	move	*a13(plyr_o1dist),a14
	subi	50,a14
	jrgt	tuubo1far				;Too far?
	move	*a13(plyr_o1dir),a2
	sub	a3,a2
	abs	a2
	cmpi	040H,a2
	jrle	tuubo1dsml
	subi	080H,a2
	abs	a2
tuubo1dsml	subk	16,a2
	jrlt	tuubpusho				;In front of me?
tuubo1far
	move	*a13(plyr_o2dist),a14
	subi	50,a14
	jrgt	tuubnopush				;Too far?
	move	*a13(plyr_o2dir),a2
	sub	a3,a2
	abs	a2
	cmpi	040H,a2
	jrle	tuubo2dsml
	subi	080H,a2
	abs	a2
tuubo2dsml	subk	16,a2
	jrge	tuubnopush				;!In front?
tuubpusho
	movi	99,a0
	callr	rndrng0

	move	*a13(plyr_ptsdown),a14
	move	*a13(plyr_d_skill),a1
	add	a1,a14
	cmpi	15,a14
	jrle	tuubmxdnok
	movk	15,a14
tuubmxdnok	addk	15,a14
	jrge	tuubdnok
	clr	a14
tuubdnok	sll	4,a14
	addi	tuubp_t,a14
	move	*a14,a1

	cmp	a1,a0
	jrge	tuubnewseek			;Skip push?

	move	*a11,a0				;Push
	ori	BUT2_M<<8|BUT2_M|BUT3_M,a0
	move	a0,*a11
	jruc	tuubx
tuubnopush


	movi	07fH,a0
	callr	rnd
	jrz	tuubnewseek

	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	tuubseek

tuubnewseek
	move	*a13(plyr_newdir),a0
	jrnn	tuubcontsk				;Turning?

	movk	16,a0
	callr	rndrng0
	sll	5,a0				;*32
	addi	tuubseek_t,a0

	move	*a0+,a1
	move	*a13(plyr_num),a14
	subk	2,a14
	jrge	tuublft
	neg	a1
tuublft	addi	WRLDMID,a1
	move	a1,*a13(plyr_d_seekx)

	move	*a0+,a1
	move	a1,*a13(plyr_d_seeky)

	movi	TSEC*3,a0
	callr	rndrng0
	addk	TSEC/2,a0

tuubseek	move	a0,*a13(plyr_d_seekcnt)
tuubcontsk
	move	*a13(plyr_d_seekx),a0
	move	*a13(plyr_d_seeky),a1
	callr	drone_seekxy
	jrnz	tuubnotthere

	movk	01fH,a0				;3%
	callr	rnd
	jrnz	tuubx
	clr	a0
	move	a0,*a13(plyr_d_seekcnt)

tuubnotthere


tuubx
	rets


tuubmdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	40,40,40,35,30		;10-6
	.word	25,22,20,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

tuubp_t					;% to push
	.word	1,1,1,1,1
	.word	2,2,2,2,3
	.word	3,3,4,4,5
	.word	5
	.word	5,6,8,10,13
	.word	15,17,18,20,20
	.word	25,30,35,40,50


	.asg	CZMID,Z
tuubseek_t
	.word	-320,Z-160, -250,Z-160, -120,Z-100	;3ptrs
	.word	-120,Z
	.word	-120,Z+100, -250,Z+160, -320,Z+160

	.word	-320,Z-100, -250,Z-80, -200,Z-50	;2ptrs
	.word	-180,Z
	.word	-200,Z+50, -250,Z+80, -320,Z+100

	.word	-310,Z-10, -305,Z, -310,Z+10



 SUBRP	drone_pass

	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	uslmx			;Teammate is a human?

	move	*a13(plyr_tmdist),a0
	addk	10,a0
	move	*a13(plyr_o1dist),a1
	cmp	a1,a0
	jrlt	uslmo1ok

	move	*a13(plyr_tmdir),a14
	move	*a13(plyr_o1dir),a1
	sub	a14,a1
	abs	a1
	cmpi	64,a1
	jrle	uslmdsml
	subi	128,a1
	abs	a1
uslmdsml	subk	16,a1
	jrlt	uslmx

uslmo1ok
	move	*a13(plyr_o2dist),a1
	cmp	a1,a0
	jrlt	uslmo2ok

	move	*a13(plyr_tmdir),a14
	move	*a13(plyr_o2dir),a1
	sub	a14,a1
	abs	a1
	cmpi	64,a1
	jrle	uslmdsml2
	subi	128,a1
	abs	a1
uslmdsml2	subk	16,a1
	jrlt	uslmx

uslmo2ok
	move	*a13(plyr_tmproc_p),a1,L
	move	*a1(plyr_seq),a0
	subk	RUNDRIBTURB_SEQ,a0
	jrhi	uslmx

	move	*a11,a0			;>Pass
	ori	BUT2_M<<8|BUT2_M|BUT3_M,a0
	move	a0,*a11

	rets

uslmx	clr	a0
	rets



 SUBRP	drone_defence

	PUSH	a7


	move	@ballpnum,a5
	sll	5,a5
	addi	plyrproc_t,a5
	move	*a5,a5,L		;A5=*Proc of opponent with ball


	callr	drone_getcurskillo
	move	a0,a7			;A7=Ptsdn+skill for indexing (*16)


	clr	a0
	move	a0,*a13(plyr_d_cflgs)

	move	*a13(plyr_d_mode),a14
	jrn	gwzminmd			;Already in mode?

	subk	1,a0			;-1
	move	a0,*a13(plyr_d_mode)

	move	a7,a14
	addi	gwzmmdsk_t,a14
	move	*a14,a0
	move	a0,a2
	srl	2,a2			;/4

	callr	rndrng0
	addk	1,a0
	add	a2,a0
	move	a0,*a13(plyr_d_seekcnt)
	
gwzminmd



	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	gwzmseek

	move	*a13(plyr_num),a2
	XORK	2,a2
	move	a2,a4
	sll	5,a4			;*32
	addi	plyrproc_t,a4
	move	*a4,a4,L
	cmp	a5,a4
	jreq	gwzmguard			;I'm on guy with ball?

	move	*a5(plyr_ohpdist),a0
	cmpi	300,a0
	jrgt	gwzmguard			;Too far to worry about?

	move	*a5(plyr_seqflgs),a0
	btst	DUNK_B,a0
	jrnz	gwzmgbc			;He's dunking?

	move	*a13(plyr_tmproc_p),a3,L
	move	*a3(plyr_seq),a0
	subi	STAGGER_SEQ,a0
	jrls	gwzmtmok
	subk	FLYBACKWB2_SEQ-STAGGER_SEQ,a0
	jrls	gwzmgbc			;Teammates staggered?
gwzmtmok
	move	*a3(plyr_o1dist),a14
	move	*a3(plyr_o1dir),a1
	btst	0,a2
	jrnz	gwzmp1
	move	*a3(plyr_o2dist),a14
	move	*a3(plyr_o2dir),a1
gwzmp1
	move	*a3(plyr_hpdir),a0	;Find dir difference
	sub	a1,a0
	abs	a0
	cmpi	040H,a0
	jrle	gwzmdsml
	subi	080H,a0
	abs	a0
gwzmdsml	subk	28,a0
	jrle	gwzmgbc			;TM not between op and hoop?

	cmpi	160,a14
	jrgt	gwzmgbc			;TM not guarding?
	jruc	gwzmguard

gwzmgbc	move	a5,a4			;Guard ball carrier
gwzmguard
	move	*a4(PA8),a2,L		;*Obj

	move	*a2(OXPOS),a0
	move	*a2(OXANI+16),a14
	add	a14,a0			;X
	move	*a2(OZPOS),a1		;Z

	move	@game_time,a14,L
	cmpi	0800H,a14
	jrlt	gwzmsetseek		;Less than 8 secs?
	move	@shotimer+16,a14	;Tens
	jrnz	gwzmscok
	move	@shotimer,a14		;Ones
	subk	5,a14
	jrlt	gwzmsetseek		;Less than 5 secs?
gwzmscok
	move	*a5(plyr_seqflgs),a14
	btst	SHOOT_B,a14
	jrnz	gwzmsetseek		;Starting a shot?

	move	*a2(OXVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a0
	move	*a2(OZVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a1

	move	*a13(plyr_myhoopx),a2	;>Stay near oplyr between my basket
	movi	CZMID,a3

	add	a0,a2
	sll	1,a0
	add	a2,a0
	sra	2,a0			;/4

	add	a1,a3
	sll	1,a1
	add	a3,a1
	sra	2,a1			;/4
gwzmsetseek
	move	a0,*a13(plyr_d_seekx)
	move	a1,*a13(plyr_d_seeky)

	movk	25,a0
	move	*a5(plyr_hpdist),a1
	cmpi	310,a1
	jrge	gwzmseek			;Far from my hoop?


	move	a7,a14
	addi	gwzmskt_t,a14
	move	*a14,a0
	move	a0,a2
	srl	2,a2			;/4
	callr	rndrng0
	add	a2,a0

gwzmseek	move	a0,*a13(plyr_d_seekcnt)

	move	*a13(plyr_d_seekx),a0
	move	*a13(plyr_d_seeky),a1
	callr	drone_seekxy
	move	a0,a2



	move	*a13(plyr_num),a14	;>Get opponents proc
	addk	2,a14
	sll	32-2,a14
	srl	32-2-5,a14		;*32
	addi	plyrproc_t,a14
	move	*a14,a0,L
	move	*a0(plyr_ohpdist),a0
	subk	10,a0
	cmpi	300,a0
	jrlt	gwzmlong
	subi	80,a0			;Turbo earlier
gwzmlong
	move	*a13(plyr_hpdist),a1
	cmp	a0,a1
	jrlt	gwzmicloser
	addi	BUT3_M,a2		;Turbo
gwzmicloser


	move	*a13(plyr_balldist),a1
	cmpi	30,a1
	jrgt	gwzmpsrnd			;!In his face?

	move	@game_time,a14,L
	cmpi	0800H,a14
	jrlt	gwzmps			;Less than 8 secs?
	move	@shotimer+16,a14	;Tens
	jrnz	gwzmpsrnd
	move	@shotimer,a14		;Ones
	subk	5,a14
	jrlt	gwzmps			;Less than 5 secs?
gwzmpsrnd
	move	a7,a14
	addi	gwzmdist_t,a14
	move	*a14,a0
	cmp	a0,a1
	jrgt	gwzmskipsp			;Ball too far?

	movi	999,a0
	callr	rndrng0

	move	a7,a14
	addi	gwzmps_t,a14
	move	*a14,a1
	cmp	a1,a0
	jrge	gwzmskipsp			;Skip push?
gwzmps
	sll	32-4,a2
	srl	32-4,a2
	addk	BUT2_M,a2		;Steal

	movk	1,a0
	callr	rnd
	jrnz	gwzmskipsp			;50%?

	addi	BUT2_M<<8|BUT3_M,a2	;Push
gwzmskipsp


	move	*a13(plyr_balldist),a14	;0CHhk if I can block ball
	cmpi	65,a14
	jrge	gwzmnoblk

	move	*a5(plyr_jmpcnt),a0
	jrnz	gwzmtryblk			;Plyr with ball is in air?

	move	*a5(plyr_seqflgs),a0
	btst	SHOOT_B,a0
	jrz	gwzmnoblk			;!Starting a shot?
	movk	11,a0
	callr	rndrng0
	move	a0,a0
	jrnz	gwzmnoblk			;94% ignore?
	jruc	gwzmblk

gwzmtryblk
	movi	99,a0
	callr	rndrng0
	move	a7,a14
	addi	gwzmblk_t,a14
	move	*a14,a1
	cmp	a1,a0
	jrge	gwzmnoblk

gwzmblk	ORK	BUT1_M,a2		;Block
gwzmnoblk
	move	a2,*a11

	PULL	a7
	rets


gwzmmdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	50,50,45,45,40		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

gwzmskt_t					;Max seek time
	.word	60,60,58,55,52
	.word	48,44,40,36,32
	.word	28,24,22,18,16
	.word	14			;Even score
	.word	10,8,6,4,4
	.word	4,3,3,3,2
	.word	2,2,2,2,1

gwzmdist_t					;Max dist to try push/steal
	.word	110,100,100,100,100
	.word	90,90,80,80,80,80
	.word	70,70,60,60,60
	.word	50
	.word	50,50,50,50,50
	.word	50,50,50,50,50
	.word	50,50,50,50,50
gwzmps_t					;% to push/steal
	.word	2,2,2,2,2
	.word	3,3,3,3,3
	.word	4,4,4,5,5
	.word	6
	.word	6,6,8,10,13
	.word	15,17,18,20,30
	.word	40,60,80,150,250
gwzmblk_t					;% to block
	.word	1,1,2,3,3
	.word	3,3,4,4,5
	.word	6,7,8,10,12
	.word	14
	.word	16,18,20,25,30
	.word	35,40,45,50,50
	.word	50,50,50,50,50



 SUBR	drone_setuptob

	move	@drone2on,b0
	jrnz	drone2_setuptob

	PUSH	a2,a3,a4,a13

	movk	4,a4
	movi	plyrproc_t,a3

bluelp	move	*a3+,a13,L
	move	*a13(plyr_ownball),a14
	jrnz	bluenxt			;Offense?

	movi	-1,a14
	move	a14,*a13(plyr_d_mode)	;Defense

	callr	drone_getcurskillo

	addi	bluemdsk_t,a0
	move	*a0,a0
	move	a0,a2
	srl	2,a2			;/4

	callr	rndrng0
	addk	1,a0
	add	a0,a2

	movk	1,a0
	callr	rnd
	jrnz	bluesk
	srl	1,a2
bluesk
	move	a2,*a13(plyr_d_seekcnt)

bluenxt
	dsj	a4,bluelp

	PULL	a2,a3,a4,a13
	rets

bluemdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	50,50,45,45,40		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15



 SUBRP	drone_chaseball

	move	*a13(plyr_rcvpass),a14
	jrgt	drone_offwoball		;I'm rcving ball?

	move	*a13(plyr_tmproc_p),a2,L
	move	*a2(plyr_rcvpass),a14
	jrgt	drone_offwoball		;Teammate rcving ball?


	clr	a0
	move	a0,*a13(plyr_d_cflgs)

	move	*a13(plyr_d_mode),a14
	jrz	eduxinmd			;Already in mode?

	move	a0,*a13(plyr_d_mode)

	move	*a13(plyr_ptsdown),a14
	move	*a13(plyr_d_skill),a0
	add	a0,a14
	cmpi	15,a14
	jrle	eduxmxdnok
	movk	15,a14
eduxmxdnok	addk	15,a14
	jrge	eduxdnok
	clr	a14
eduxdnok	sll	4,a14
	addi	eduxmdsk_t,a14
	move	*a14,a0
	callr	rndrng0
	addk	2,a0
	move	a0,*a13(plyr_d_seekcnt)

eduxinmd

	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	eduxseek

	move	*a2(plyr_seq),a0
	subi	STAGGER_SEQ,a0
	jrls	eduxtmok
	subk	FLYBACKWB2_SEQ-STAGGER_SEQ,a0
	jrls	eduxchaseb			;Teammates staggered?
eduxtmok
	move	*a13(plyr_balldist),a0
	move	*a2(plyr_balldist),a14
	cmp	a14,a0
	jrlt	eduxchaseb			;I'm closer?

	movi	WRLDMID,a0
	movi	CZMID,a1
	jruc	eduxsetxz

eduxchaseb
	move	@ballobj_p,a5,L

	move	*a5(OXPOS),a0
	move	*a5(OXANI+16),a14
	add	a14,a0
	move	*a5(OXVEL),a14,L
	sra	16-3,a14		;8 ticks from now
	add	a14,a0

	move	*a5(OZPOS),a1
	move	*a5(OZVEL),a14,L
	sra	16-3,a14		;8 ticks from now
	add	a14,a1
eduxsetxz
	move	a0,*a13(plyr_d_seekx)
	move	a1,*a13(plyr_d_seeky)


	move	*a13(plyr_ptsdown),a14
	move	*a13(plyr_d_skill),a0
	add	a0,a14
	cmpi	15,a14
	jrle	eduxmxdnok2
	movk	15,a14
eduxmxdnok2
	addk	15,a14
	jrge	eduxdnok2
	clr	a14
eduxdnok2	sll	4,a14
	addi	eduxskt_t,a14
	move	*a14,a0

	callr	rndrng0
	addk	5,a0
eduxseek	move	a0,*a13(plyr_d_seekcnt)

	move	*a13(plyr_d_seekx),a0
	move	*a13(plyr_d_seeky),a1
	callr	drone_seekxy


	move	*a13(plyr_balldist),a2	;0CHhk if I can jump at ball
	subk	30,a2
	jrge	eduxnojmp

	move	*a5(OYPOS),a0
	addk	25,a0
	move	*a8(OYPOS),a1
	cmp	a1,a0
	jrlt	eduxnojmp

	movk	7,a0
	callr	rnd
	jrnz	eduxnojmp			;87%?

	move	*a11,a0			;Jmp
	ORK	BUT1_M,a14
	move	a0,*a11
eduxnojmp

	rets

eduxmdsk_t					;Mode switch max seek time
	.word	90,90,90,90,80		;Up 15-11
	.word	80,70,60,55,50		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

eduxskt_t					;Max seek time
	.word	50,50,45,45,45
	.word	40,40,30,30,20
	.word	20,16,14,13,12
	.word	12
	.word	12,11,10,10,9
	.word	8,5,4,3,2
	.word	1,1,1,1,1



 SUBRP	drone_seekxy

	move	a2,b0

	move	*a8(OXPOS),a2
	move	*a8(OXANI+16),a14
	add	a14,a2
	sub	a0,a2

	clr	a0

	move	a2,a14
	abs	a2
	subk	10,a2
	jrle	tfdmonx
	move	a14,a14
	jrlt	tfdmnolft
	subk	4,a0			;Left

tfdmnolft	addk	8,a0			;Rgt
tfdmonx
	move	*a8(OZPOS),a2

	sub	a1,a2
	move	a2,a14
	abs	a2
	subk	10,a2
	jrle	tfdmonz
	move	a14,a14
	jrlt	tfdmnoup
	subk	1,a0			;Up

tfdmnoup	addk	2,a0			;Dn
tfdmonz
	move	a0,*a11

	move	b0,a2
	move	a0,a0
	rets



 SUBRP	drone_getcurskillo

	move	*a13(plyr_ptsdown),a0
	move	*a13(plyr_d_skill),a14
	add	a14,a0
	cmpi	15,a0
	jrle	cteymxdnok
	movk	15,a0
cteymxdnok	addk	15,a0
	jrge	cteydnok
	clr	a0
cteydnok	sll	4,a0			;A7=Ptsdn+skill for indexing (*16)

	rets




 SUBR	drone_adjskill

	move	@drone2on,b0
	jrnz	drone2_adjskill


	PUSH	a2,a3,a4

	move	a0,a0
	jrnz	oueyx

	movi	plyrproc_t,a4
	movk	4,b0
oueylp
	move	*a4+,a3,L

	move	*a3(plyr_d_skill),a2

	move	*a3(plyr_ptsdown),a14
	cmpi	15,a14
	jrle	oueymxdnok
	movk	15,a14
oueymxdnok	addk	15,a14
	jrge	oueydnok
	clr	a14
oueydnok	sll	4,a14
	addi	oueyadj_t,a14
	move	*a14,a14
	add	a14,a2

	movk	ADJDIFF,a0		;Get difficulty level
	calla	GET_ADJ
	subk	3,a0			;-2 to 2
	sll	1,a0			;*2

	move	@gmqrtr,a14		;0CHhk quarter minimum
	cmpi	2,a14
	jrle	oueyqok
	movk	2,a14
oueyqok	sll	4,a14
	addi	oueymin_t,a14
	move	*a14,a14
	add	a0,a14
	cmp	a14,a2
	jrge	oueyminok
	move	a14,a2
oueyminok
	move	*a3(plyr_num),a1
	XORK	1,a1
	move	@PSTATUS,a14
	btst	a1,a14
	jrnz	oueytminok			;Teammate is human?

	move	@team1,a14		;0CHhk team minimum
	cmpi	3,b0
	jrge	oueyt1
	move	@team2,a14
oueyt1	sll	3+2,a14

	move	@gmqrtr,a1
	cmpi	3,a1
	jrle	oueyqok2
	movk	3,a1
oueyqok2	sll	3,a1
	add	a1,a14

	addi	oueytdmin_t-8,a14
	movb	*a14,a14
	add	a0,a14
	cmp	a14,a2
	jrge	oueytminok
	move	a14,a2
oueytminok
	move	a2,*a3(plyr_d_skill)

	dsj	b0,oueylp

oueyx	PULL	a2,a3,a4
	rets

oueyadj_t	.word	-5,-5,-5,-5,-5
	.word	-5,-5,-5,-4,-3
	.word	-2,-1,0,0,0
	.word	0
	.word	1,1,2,2,3
	.word	3,4,4,5,5
	.word	5,6,6,6,7

oueymin_t	.word	-8,-6,-4

oueytdmin_t
	.byte	-8,-6,-4,0	;ATL
	.byte	-8,-6,-4,0	;BOST
	.byte	-8,-6,-4,0	;CHAR
	.byte	6,9,12,14	;CHI (1)
	.byte	-8,-6,-4,0	;CLEV
	.byte	-8,-6,-4,0	;DAL
	.byte	-8,-6,-4,0	;DEN
	.byte	-8,-6,-4,0	;DET
	.byte	-8,-6,-4,0	;GOLD
	.byte	-8,-6,-4,0	;HOU
	.byte	-8,-6,-4,0	;IND
	.byte	-8,-6,-4,0	;LAC
	.byte	-8,-6,-4,0	;LAL
	.byte	-8,-6,-4,0	;MIAMI
	.byte	-8,-6,-4,0	;MILW
	.byte	-8,-6,-4,0	;MINN
	.byte	-8,-6,-4,0	;NJ
	.byte	1,3,4,5		;NY (4)
	.byte	-8,-6,-4,0	;ORL
	.byte	-8,-6,-4,0	;PHIL
	.byte	4,6,7,8		;PHX (2)
	.byte	2,4,5,6		;PORT (3)
	.byte	-8,-6,-4,0	;SAC
	.byte	-8,-6,-4,0	;SANANT
	.byte	-8,-6,-4,0	;SEA
	.byte	0,2,3,4		;UTAH (5)
	.byte	-8,-6,-4,0	;WASH
	.even



 SUBRP	rnd

	move	@RAND,a1,L
	rl	a1,a1
	move	@HCOUNT,a14
	rl	a14,a1
	add	sp,a1
	move	a1,@RAND,L

	and	a1,a0
	rets



 SUBRP	rndrng0

	move	@RAND,a1,L
	rl	a1,a1
	move	@HCOUNT,a14
	rl	a14,a1
	add	sp,a1
	move	a1,@RAND,L

	addk	1,a0
	mpyu	a1,a0		;Condition codes not valid!

	rets




	.end

