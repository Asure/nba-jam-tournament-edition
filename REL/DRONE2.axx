	.file	"drone2.asm"
	.title	"basketball drone code"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"mproc.equ"		;Mproc equates
	.include	"disp.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"audit.equ"
	.include	"shawn.hdr"		;Macros
	.asg		0,SEQT
	.include	"plyr.equ"





	.ref	plyrobj_t,plyrproc_t
	.ref	ballobj_p
	.ref	ballpnum,ballpnumshot
	.ref	plyrnumonfire
	.ref	seekdirdist_obxz128

	.ref	game_time,gmqrtr
	.ref	shotimer

	.ref	team1,team2

	.ref	RNDPER
	.ref	PSTATUS
	.ref	GET_ADJ





	.bss	drnzzcnt	,16	;Drone zigzag mode cntdn
	.bss	drnzzmode	,16	;Drone zigzag mode (0-?)
	BSSX	drone2on	,16	;!0=Use drone version2 code




	.text




 SUBR	drone2_main


	move	@ballpnum,a14
	jrn	drone_chaseball		;No owner?

	move	*a13(plyr_ownball),a1
	jrz	drone_defence		;We don't have ball?
	jrn	drone_offwoball		;Teammate has ball?



	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrz	kqnvtmdrone		;Teammate is a drone?

	move	*a13(plyr_d_cflgs),a2
	btst	DRN_PASS_B,a2
	jrz	kqnvnopass

	movi	BUT2_M<<8+BUT2_M,a14	;>Make him pass
	jruc	kqnvdocmd

kqnvnopass
	btst	DRN_SHOOT_B,a2
	jrz	kqnvnoshoot
	movi	BUT1_M|BUT3_M,a14	;>Make him shoot
kqnvdocmd
	move	*a11,a0
	sll	32-4,a0
	srl	32-4,a0
	ori	BUT3_M,a0		;+turbo
kqnvnoturb
	or	a14,a0
	move	a0,*a11
	clr	a0
	move	a0,*a13(plyr_d_cflgs)
	jruc	kqnvx

kqnvnoshoot
kqnvtmdrone

	move	*a13(plyr_d_mode),a14
	subk	2,a14
	jrge	kqnvinmd			;Already in mode?

	movk	2,a1			;Offense with ball
	move	a1,*a13(plyr_d_mode)
	movk	1,a1
	jruc	kqnvsetskc
kqnvinmd


	move	*a13(plyr_d_seekcnt),a1	;>Update tob mode
	jrle	kqnvnotob
kqnvsetskc	subk	1,a1
	move	a1,*a13(plyr_d_seekcnt)
	jrgt	kqnvnotob
	move	*a13(plyr_ohoopx),*a13(plyr_d_seekx)
	movi	CZMID,a0
	move	a0,*a13(plyr_d_seeky)
kqnvnotob

	move	*a13(plyr_seqflgs),a2
	btst	PASS_B,a2
	jrnz	kqnvkilbuts

	move	*a13(plyr_jmpcnt),a14
	jrnz	kqnvinjmp

	move	*a11,a0
	btst	BUT1_B,a0
	jrnz	kqnvfake			;Shoot button down?


	btst	SHOOT_B,a2
	jrnz	kqnvinjmp


	move	*a13(plyr_dribmode),a14
	jrn	kqnvnodrib

	move	*a13(plyr_num),a14	;0CHhk for breakaway
	srl	1,a14
	movk	1,a0
	xor	a0,a14
	sll	6,a14			;*64
	addi	plyrproc_t,a14
	move	*a14+,a2,L
	move	*a14+,a3,L

	move	*a2(plyr_hpdist),a2
	move	*a3(plyr_hpdist),a3
	move	*a13(plyr_ohpdist),a4

	cmp	a4,a2
	jrlt	kqnvshootrnd		;He's closer?
	cmp	a4,a3
	jrlt	kqnvshootrnd		;He's closer?

	move	*a13(plyr_ohoopx),a0
	movi	CZMID,a1
	callr	drone_seekxy
	move	*a13(plyr_d_skill),a14
	cmpi	-11-3,a14
	jrle	kqnvnoturb2
	ori	BUT3_M,a0		;Push turbo
	move	a0,*a11
kqnvnoturb2

	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	kqnvshoot2			;Less than 2 secs?
	move	@shotimer+16,a1		;Tens
	jrnz	kqnvscok
	move	@shotimer,a1		;Ones
	subk	2,a1
	jrlt	kqnvshoot2			;Less than 2 secs?
kqnvscok
	callr	drone_chk3ptr
	jrnz	kqnvshoot2			;Need a 3?

	move	*a13(plyr_ohpdist),a14
	cmpi	170,a14
	jrge	kqnvx			;Too far?
	cmpi	50,a14
	jrlt	kqnvshoot2			;Close?
	movk	01fH,a0
	callr	rnd
	jrnz	kqnvx
	jruc	kqnvshoot2


kqnvnodrib					;0CaHn't dribble
	move	*a13(plyr_ohpdist),a0
	cmpi	230,a0
	jrlt	kqnvshoot2
	callr	drone_pass
	jrnz	kqnvx			;Pass OK?
	movk	01fH,a0
	callr	rnd
	jrnz	kqnvx			;97%?
	jruc	kqnvshoot2


kqnvshootrnd
	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	kqnvshoot2			;Less than 2 secs?
	move	@shotimer+16,a1
	jrnz	kqnvscok2
	move	@shotimer,a1
	subk	2,a1
	jrlt	kqnvshoot2			;Less than 2 secs?
kqnvscok2
	PUSH	a6,a7
	move	*a13(plyr_ohoopx),a6
	movi	CZMID,a7
	calla	seekdirdist_obxz128
	PULL	a6,a7

	move	*a13(plyr_o1dist),a14
	cmpi	80,a14
	jrgt	kqnvo1dok			;He's too far?
	cmp	a14,a1
	jrlt	kqnvo1dok			;I'm closer?
	move	*a13(plyr_o1dir),a2
	sub	a0,a2
	abs	a2
	cmpi	040H,a2
	jrle	kqnvo1dsml
	subi	080H,a2
	abs	a2
kqnvo1dsml	subk	32,a2
	jrlt	kqnvgoaround		;In front of me?
kqnvo1dok
	move	*a13(plyr_o2dist),a14
	cmpi	80,a14
	jrgt	kqnvo2dok			;He's too far?
	cmp	a14,a1
	jrlt	kqnvo2dok			;I'm closer?
	move	*a13(plyr_o2dir),a2
	sub	a0,a2
	abs	a2
	cmpi	040H,a2
	jrle	kqnvo2dsml
	subi	080H,a2
	abs	a2
kqnvo2dsml	subk	32,a2
	jrlt	kqnvgoaround		;In front of me?
kqnvo2dok
	jruc	kqnvrunath

kqnvgoaround				;>Opponent in my way
	move	*a13(plyr_tmdist),a0
	cmpi	70,a0
	jrlt	kqnvgoa			;Teammate too close?

	callr	drone_pass
	jrnz	kqnvx			;Pass OK?

kqnvgoa
	movi	drnzzcnt,a2
	move	*a2,a0
	subk	1,a0
	jrgt	kqnvzzsame

	movk	3,a0			;New mode
	callr	rnd
	move	a0,*a2(drnzzmode-drnzzcnt)
	movi	TSEC-10,a0
	callr	rndrng0
	addk	10,a0
kqnvzzsame
	move	a0,*a2

	callr	drone_seek
	sll	3,a0			;*8
	addi	kqnvjbits_t,a0

	move	*a2(drnzzmode-drnzzcnt),a14
	sll	4+3,a14			;*16*8
	add	a14,a0
kqnvmd1	movb	*a0,a0
	move	a0,*a11

	cmpi	80,a4
	jrlt	kqnvshoot2			;Close enough for jam?

	jruc	kqnvtryshot


kqnvrunath					;>I have a clr path to hoop!
	move	*a13(plyr_o1dist),a14
	cmpi	65,a14
	jrlt	kqnvgoa			;He's too close?
	move	*a13(plyr_o2dist),a14
	cmpi	65,a14
	jrlt	kqnvgoa			;He's too close?

	callr	drone_seek
	move	*a13(plyr_d_skill),a14
	cmpi	-11-3,a14
	jrle	kqnvtryshot
	ori	BUT3_M,a0		;Turbo
	move	a0,*a11


kqnvtryshot
	cmpi	50,a4
	jrlt	kqnvshoot2			;Close enough for jam?

	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	kqnvx			;Teammate is a human?

	callr	drone_chk3ptr
	jrnz	kqnvshoot2			;Need a 3?

	movi	50,a0
	move	*a13(plyr_d_skill),a14
	cmpi	-8,a14
	jrgt	kqnvrndsht
	movk	30,a0
kqnvrndsht	callr	rndrng0
	move	a0,a0
	jrnz	kqnvx

	cmpi	250,a4
	jrge	kqnvx			;Too far?


kqnvshoot2	move	*a11,a0			;>Shoot
	ori	BUT1_M|BUT3_M,a0
	move	a0,*a11
	jruc	kqnvx



kqnvfake					;>Jumping but still on gnd
	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	kqnvx			;Teammate is a human?
	movk	01fH,a0
	callr	rnd
	jrnz	kqnvx

	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	kqnvx			;Less than 2 secs?
	move	@shotimer+16,a1		;Tens
	jrnz	kqnvfk
	move	@shotimer,a1		;Ones
	subk	2,a1
	jrlt	kqnvx			;Less than 2 secs?
kqnvfk
	jruc	kqnvkilbuts


kqnvinjmp
	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrz	kqnvtmdrn			;Teammate is a drone?

	move	*a13(plyr_tmproc_p),a0,L
	move	*a0(PA11),a0,L
	move	*a0,a0			;Get teammates ctrl bits
	btst	BUT1_B,a0
	jrnz	kqnvx			;Holding shoot button?
	jruc	kqnvkilbuts
kqnvtmdrn
	move	*a3(plyr_seqflgs),a0
	btst	BLOCKREB_B,a0
	jrnz	kqnvkilbuts		;Got a rebound?

	movk	15,a0
	callr	rndrng0
	move	a0,a0
	jrz	kqnvkilbuts		;Cause shoot?

	movk	7,a0
	callr	rnd
	jrnz	kqnvx			;88%?

	move	*a13(plyr_ptsdown),a14
	addk	5,a14
	jrlt	kqnvpss			;Winning by 05H?
	move	*a13(plyr_seqflgs),a14
	btst	DUNK_B,a14
	jrnz	kqnvx			;In a dunk?
	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	kqnvx			;Less than 2 secs?

kqnvpss	callr	drone_pass

	jruc	kqnvx



kqnvkilbuts
	clr	a0			;>Let go of shoot button
	move	a0,*a11


kqnvx	rets


kqnvjbits_t
	.byte	0,JOYL_M,JOYR_M,0			;90~ clockwise
	.byte	JOYD_M,JOYD_M|JOYL_M,JOYD_M|JOYR_M,0
	.byte	JOYU_M,JOYU_M|JOYL_M,JOYU_M|JOYR_M,0
	.byte	0,0,0,0

	.byte	0,JOYR_M,JOYL_M,0			;90~ cntr clkwise
	.byte	JOYU_M,JOYU_M|JOYR_M,JOYU_M|JOYL_M,0
	.byte	JOYD_M,JOYD_M|JOYR_M,JOYD_M|JOYL_M,0
	.byte	0,0,0,0

	.byte	0,JOYD_M|JOYL_M,JOYU_M|JOYR_M,0		;135~ clkwise
	.byte	JOYD_M|JOYR_M,JOYD_M,JOYR_M,0
	.byte	JOYU_M|JOYL_M,JOYL_M,JOYU_M,0
	.byte	0,0,0,0

	.byte	0,JOYD_M|JOYR_M,JOYU_M|JOYL_M,0		;135~ cntr clkwise
	.byte	JOYU_M|JOYR_M,JOYR_M,JOYU_M,0
	.byte	JOYD_M|JOYL_M,JOYD_M,JOYL_M,0
	.byte	0,0,0,0



 SUBRP	drone_chk3ptr

	cmpi	290,a4
	jrgt	ytwvx0			;Too far?

	move	*a13(plyr_num),a1
	move	@plyrnumonfire,a0
	cmp	a1,a0
	jreq	ytwvx1			;I'm on fire?

	cmpi	230,a4
	jrlt	ytwvx0			;Too close?

	move	@PSTATUS,a0		;Plyr start bits 0-3
	XORK	1,a1
	btst	a1,a0
	jrnz	ytwvx0			;Teammate is a human?

	movk	6,a1
	move	@game_time,a0,L
	cmpi	01010000H,a0
	jrgt	ytwvhvtime			;Enough time?
	movk	3,a1
ytwvhvtime
	move	*a13(plyr_ptsdown),a14
	cmp	a1,a14
	jrlt	ytwvx0

	cmpi	040000H,a0
	jrlt	ytwvx1			;Less than 40 secs?

	move	*a13(plyr_o1dist),a14
	cmpi	70,a14
	jrlt	ytwvrndsht			;He's close?
	move	*a13(plyr_o2dist),a14
	cmpi	70,a14
	jrge	ytwvx1			;He's far?

ytwvrndsht	movk	8,a0
	callr	rndrng0
	move	a0,a0
	jrnz	ytwvx0

ytwvx1	addk	1,a0
	rets

ytwvx0	clr	a0
	rets



 SUBRP	drone_offwoball

	clr	a0
	move	a0,*a13(plyr_d_cflgs)


	move	*a13(plyr_d_mode),a14
	subk	1,a14
	jreq	aavcinmd			;Already in mode?

	movk	1,a0			;Offense wo ball
	move	a0,*a13(plyr_d_mode)

	callr	drone_getcurskillo
	addi	aavcmdsk_t,a0
	move	*a0,a0
	callr	rndrng0
	addk	1,a0
	move	a0,*a13(plyr_d_seekcnt)
aavcinmd


	move	*a13(plyr_dir),a3

	move	*a13(plyr_o1dist),a14
	subi	50,a14
	jrgt	aavco1far				;Too far?
	move	*a13(plyr_o1dir),a2
	sub	a3,a2
	abs	a2
	cmpi	040H,a2
	jrle	aavco1dsml
	subi	080H,a2
	abs	a2
aavco1dsml	subk	16,a2
	jrlt	aavcpusho				;In front of me?
aavco1far
	move	*a13(plyr_o2dist),a14
	subi	50,a14
	jrgt	aavcnopush				;Too far?
	move	*a13(plyr_o2dir),a2
	sub	a3,a2
	abs	a2
	cmpi	040H,a2
	jrle	aavco2dsml
	subi	080H,a2
	abs	a2
aavco2dsml	subk	16,a2
	jrge	aavcnopush				;!In front?
aavcpusho
	movi	99,a0
	callr	rndrng0

	move	*a13(plyr_ptsdown),a14
	move	*a13(plyr_d_skill),a1
	add	a1,a14
	cmpi	15,a14
	jrle	aavcmxdnok
	movk	15,a14
aavcmxdnok	addk	15,a14
	jrge	aavcdnok
	clr	a14
aavcdnok	sll	4,a14
	addi	aavcp_t,a14
	move	*a14,a1

	cmp	a1,a0
	jrge	aavcnewseek			;Skip push?

	move	*a11,a0				;Push
	ori	BUT2_M<<8|BUT2_M|BUT3_M,a0
	move	a0,*a11
	jruc	aavcx
aavcnopush


	movi	07fH,a0
	callr	rnd
	jrz	aavcnewseek

	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	aavcseek

aavcnewseek
	move	*a13(plyr_newdir),a0
	jrnn	aavccontsk				;Turning?

	movk	16,a0
	callr	rndrng0
	sll	5,a0				;*32
	addi	aavcseek_t,a0

	move	*a0+,a1
	move	*a13(plyr_num),a14
	subk	2,a14
	jrge	aavclft
	neg	a1
aavclft	addi	WRLDMID,a1
	move	a1,*a13(plyr_d_seekx)

	move	*a0+,a1
	move	a1,*a13(plyr_d_seeky)

	movi	TSEC*3,a0
	callr	rndrng0
	addk	TSEC/2,a0

aavcseek	move	a0,*a13(plyr_d_seekcnt)
aavccontsk
	callr	drone_seek
	jrnz	aavcnotthere

	movk	01fH,a0				;3%
	callr	rnd
	jrnz	aavcx
	clr	a0
	move	a0,*a13(plyr_d_seekcnt)

aavcnotthere


aavcx
	rets


aavcmdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	40,40,40,35,30		;10-6
	.word	25,22,20,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

aavcp_t					;% to push
	.word	1,1,1,1,1
	.word	2,2,2,2,3
	.word	3,3,4,4,5
	.word	5
	.word	5,6,8,10,13
	.word	15,17,18,20,20
	.word	25,30,35,40,50


	.asg	CZMID,Z
aavcseek_t
	.word	-320,Z-160, -250,Z-160, -120,Z-100	;3ptrs
	.word	-120,Z
	.word	-120,Z+100, -250,Z+160, -320,Z+160

	.word	-320,Z-100, -250,Z-80, -200,Z-50	;2ptrs
	.word	-180,Z
	.word	-200,Z+50, -250,Z+80, -320,Z+100

	.word	-310,Z-10, -300,Z, -310,Z+10



 SUBRP	drone_pass

	move	@PSTATUS,a0			;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	oedxx				;Teammate is a human?

	move	*a13(plyr_tmdist),a0
	addk	10,a0

	move	*a13(plyr_o1dist),a1		;0CHhk if o1 in my way
	cmp	a1,a0
	jrlt	oedxo1ok

	move	*a13(plyr_tmdir),a14
	move	*a13(plyr_o1dir),a1
	sub	a14,a1
	abs	a1
	cmpi	64,a1
	jrle	oedxdsml
	subi	128,a1
	abs	a1
oedxdsml	subk	16,a1
	jrlt	oedxinway
oedxo1ok

	move	*a13(plyr_o2dist),a1		;0CHhk if o2 in my way
	cmp	a1,a0
	jrlt	oedxo2ok

	move	*a13(plyr_tmdir),a14
	move	*a13(plyr_o2dir),a1
	sub	a14,a1
	abs	a1
	cmpi	64,a1
	jrle	oedxdsml2
	subi	128,a1
	abs	a1
oedxdsml2	subk	16,a1
	jrlt	oedxinway
oedxo2ok

	move	*a13(plyr_tmproc_p),a1,L
oedxtmclos
	move	*a1(plyr_seq),a0
	subk	RUNDRIBTURB_SEQ,a0
	jrhi	oedxx				;Tm is doing something?

	move	*a11,a0				;>Pass
	ori	BUT2_M<<8|BUT2_M|BUT3_M,a0
	move	a0,*a11

	rets

oedxinway
	move	*a13(plyr_ohpdist),a1
	cmpi	250,a1
	jrlt	oedxx
	move	*a13(plyr_tmproc_p),a1,L
	move	*a1(plyr_ohpdist),a0
	cmpi	240,a0
	jrlt	oedxtmclos				;Teammate is close to hoop?

oedxx	clr	a0
	rets



 SUBRP	drone_defence

	PUSH	a7,a10


	move	@ballpnum,a5
	sll	5,a5
	addi	plyrproc_t,a5
	move	*a5,a5,L		;A5=*Proc of opponent with ball


	callr	drone_getcurskillo
	move	a0,a7			;A7=Ptsdn+skill for indexing (*16)


	clr	a0
	move	a0,*a13(plyr_d_cflgs)

	move	*a13(plyr_d_mode),a14
	jrn	qjcdinmd			;Already in mode?

	subk	1,a0			;-1
	move	a0,*a13(plyr_d_mode)

	move	a7,a14
	addi	qjcdmdsk_t,a14
	move	*a14,a0
	move	a0,a2
	srl	2,a2			;/4

	callr	rndrng0
	addk	1,a0
	add	a2,a0
	move	a0,*a13(plyr_d_seekcnt)

	clr	a0
	move	a0,*a9(pld_d_nastycnt)
qjcdinmd


	movk	2,a10			;>Update nasty mode
	move	@game_time,a14,L

	cmpi	0800H,a14
	jrlt	qjcdnasty			;Less than 8 secs?

	move	@gmqrtr,a2
	subk	3,a2
	jrlt	qjcdchkst
	move	*a13(plyr_ptsdown),a1
	addk	3,a1
	jrle	qjcdchkst			;Winning by 3 or more?
	cmpi	030000H,a14
	jrlt	qjcdnasty			;Less than 30 secs?

qjcdchkst	move	@shotimer+16,a14	;Tens
	jrnz	qjcdscok
	move	@shotimer,a14		;Ones
	subk	5,a14
	jrlt	qjcdnasty			;Less than 5 secs?

qjcdscok	move	*a9(pld_d_nastycnt),a10
	jrgt	qjcdnaston

	movi	999,a0
	callr	rndrng0

	move	a7,a14
	addi	qjcdnast_t,a14
	move	*a14,a1
	cmp	a1,a0
	jrge	qjcdnonast			;No nasty?

	movi	TSEC,a10

qjcdnaston	subk	1,a10
qjcdnasty	move	a10,*a9(pld_d_nastycnt)
qjcdnonast


	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	qjcdseek

	move	*a13(plyr_num),a2
	XORK	2,a2
	move	a2,a4
	sll	5,a4			;*32
	addi	plyrproc_t,a4
	move	*a4,a4,L
	cmp	a5,a4
	jreq	qjcdguard			;I'm on guy with ball?

	move	*a5(plyr_ohpdist),a0
	cmpi	300,a0
	jrgt	qjcdguard			;Too far to worry about?

	move	*a5(plyr_seqflgs),a0
	btst	DUNK_B,a0
	jrnz	qjcdgbc			;He's dunking?

	move	*a13(plyr_tmproc_p),a3,L
	move	*a3(plyr_seq),a0
	subi	STAGGER_SEQ,a0
	jrls	qjcdtmok
	subk	FLYBACKWB2_SEQ-STAGGER_SEQ,a0
	jrls	qjcdgbc			;Teammates staggered?
qjcdtmok
	move	*a3(plyr_o1dist),a14
	move	*a3(plyr_o1dir),a1
	btst	0,a2
	jrnz	qjcdp1
	move	*a3(plyr_o2dist),a14
	move	*a3(plyr_o2dir),a1
qjcdp1
	move	*a3(plyr_hpdir),a0	;Find dir difference
	sub	a1,a0
	abs	a0
	cmpi	040H,a0
	jrle	qjcddsml
	subi	080H,a0
	abs	a0
qjcddsml	subk	28,a0
	jrle	qjcdgbc			;TM not between op and hoop?

	cmpi	160,a14
	jrgt	qjcdgbc			;TM not guarding?
	jruc	qjcdguard

qjcdgbc	move	a5,a4			;Guard ball carrier
qjcdguard
	move	*a4(PA8),a2,L		;*Obj

	move	*a2(OXPOS),a0
	move	*a2(OXANI+16),a14
	add	a14,a0			;X
	move	*a2(OZPOS),a1		;Z

	move	*a2(OXVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a0
	move	*a2(OZVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a1

	move	a10,a10
	jrgt	qjcdsetseek		;Nasty on?

	move	*a5(plyr_seqflgs),a14
	btst	SHOOT_B,a14
	jrnz	qjcdsetseek		;Starting a shot?

	move	*a13(plyr_myhoopx),a2	;>Stay near oplyr between my basket
	movi	CZMID,a3

	add	a0,a2
	sll	1,a0
	add	a2,a0
	sra	2,a0			;/4

	add	a1,a3
	sll	1,a1
	add	a3,a1
	sra	2,a1			;/4
qjcdsetseek
	move	a0,*a13(plyr_d_seekx)
	move	a1,*a13(plyr_d_seeky)

	movk	25,a0
	move	*a5(plyr_hpdist),a1
	cmpi	320,a1
	jrge	qjcdseek			;Far from my hoop?


	move	a7,a14
	addi	qjcdskt_t,a14
	move	*a14,a0
	move	a0,a2
	srl	2,a2			;/4
	callr	rndrng0
	add	a2,a0

qjcdseek	move	a0,*a13(plyr_d_seekcnt)

	callr	drone_seek
	move	a0,a2



	move	*a13(plyr_num),a14	;>Get opponents proc
	addk	2,a14
	sll	32-2,a14
	srl	32-2-5,a14		;*32
	addi	plyrproc_t,a14
	move	*a14,a0,L
	move	*a0(plyr_ohpdist),a0
	subk	10,a0
	cmpi	300,a0
	jrlt	qjcdlong
	subi	80,a0			;Turbo earlier
qjcdlong
	move	*a13(plyr_hpdist),a1
	cmp	a0,a1
	jrlt	qjcdicloser
	addi	BUT3_M,a2		;Turbo
qjcdicloser


	move	*a13(plyr_balldist),a1
	cmpi	35,a1
	jrgt	qjcdpsrnd			;!In his face?

	move	a10,a10
	jrgt	qjcdps			;Nasty on?

qjcdpsrnd
	move	a7,a14
	addi	qjcddist_t,a14
	move	*a14,a0
	cmp	a0,a1
	jrgt	qjcdskipsp			;Ball too far?

	movi	999,a0
	callr	rndrng0

	move	a7,a14
	addi	qjcdps_t,a14
	move	*a14,a1
	cmp	a1,a0
	jrge	qjcdskipsp			;Skip push?
qjcdps
	sll	32-4,a2
	srl	32-4,a2
	addk	BUT2_M,a2		;Steal

	movk	1,a0
	callr	rnd
	jrnz	qjcdskipsp			;50%?

	addi	BUT2_M<<8|BUT3_M,a2	;Push
qjcdskipsp


	move	*a13(plyr_balldist),a14	;0CHhk if I can block ball
	cmpi	65,a14
	jrge	qjcdnoblk

	move	*a5(plyr_jmpcnt),a0
	jrnz	qjcdtryblk			;Plyr with ball is in air?

	move	*a5(plyr_seqflgs),a0
	btst	SHOOT_B,a0
	jrz	qjcdnoblk			;!Starting a shot?
	movk	11,a0
	callr	rndrng0
	move	a0,a0
	jrnz	qjcdnoblk			;94% ignore?
	jruc	qjcdblk

qjcdtryblk
	move	a10,a10
	jrgt	qjcdblk			;Nasty on?

	movi	99,a0
	callr	rndrng0
	move	a7,a14
	addi	qjcdblk_t,a14
	move	*a14,a1
	cmp	a1,a0
	jrge	qjcdnoblk

qjcdblk
	sll	32-4,a2
	srl	32-4,a2
	addk	BUT1_M,a2		;Block
qjcdnoblk

	move	a2,*a11

	PULL	a7,a10
	rets


qjcdmdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	50,50,45,45,40		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

qjcdnast_t					;% to become nasty
	.word	0,0,0,0,0
	.word	1,1,2,2,2
	.word	3,3,3,4,6
	.word	8
	.word	10,11,12,13,14
	.word	15,17,20,25,26
	.word	27,28,29,30,35
qjcdskt_t					;Max seek time
	.word	60,60,58,55,52
	.word	48,44,40,36,32
	.word	28,24,22,18,16
	.word	15			;Even score
	.word	15,15,14,14,14
	.word	14,14,14,13,13
	.word	13,13,13,12,12

qjcddist_t					;Max dist to try push/steal
	.word	110,100,100,100,100
	.word	90,90,80,80,80,80
	.word	70,70,60,60,60
	.word	50
	.word	50,50,50,50,50
	.word	50,50,50,50,50
	.word	50,50,50,50,50
qjcdps_t					;% to push/steal
	.word	1,2,2,2,2
	.word	3,3,3,3,3
	.word	4,4,4,5,5
	.word	6
	.word	6,6,8,10,13
	.word	15,17,18,20,30
	.word	40,60,80,150,250
qjcdblk_t					;% to block
	.word	1,1,2,3,3
	.word	3,3,4,4,5
	.word	6,7,8,10,12
	.word	14
	.word	16,18,20,25,30
	.word	35,40,45,50,50
	.word	50,50,50,50,50



 SUBR	drone2_setuptob

	PUSH	a2,a3,a4,a13

	movk	4,a4
	movi	plyrproc_t,a3

rqdylp	move	*a3+,a13,L
	move	*a13(PA9),a2,L

	movk	1,a0
	move	*a13(plyr_ownball),a14
	jrz	rqdydef			;Defense?
					;>Setup offense
	jrn	rqdywob
	movk	2,a0
rqdywob	move	a0,*a13(plyr_d_mode)

	movi	TSEC,a0
	callr	rndrng0
	addk	5,a0
	move	a0,*a13(plyr_d_seekcnt)

	movk	9-1,a0
	callr	rndrng0
	sll	5,a0			;*32
	addi	rqdyseek_t,a0

	move	*a0+,a1
	move	*a13(plyr_num),a14
	subk	2,a14
	jrlt	rqdylft
	neg	a1
rqdylft	addi	WRLDMID,a1
	move	a1,*a13(plyr_d_seekx)

	move	*a0+,a1
	move	a1,*a13(plyr_d_seeky)

	jruc	rqdynxt


rqdydef					;>Setup defense
	movi	-1,a14
	move	a14,*a13(plyr_d_mode)	;Defense

	clr	a0
	move	a0,*a2(pld_d_nastycnt)

	callr	drone_getcurskillo

	addi	rqdymdsk_t,a0
	move	*a0,a0
	move	a0,a2
	srl	2,a2			;/4

	callr	rndrng0
	addk	1,a0
	add	a0,a2

	movk	1,a0
	callr	rnd
	jrnz	rqdysk
	srl	1,a2
rqdysk
	move	a2,*a13(plyr_d_seekcnt)

rqdynxt
	dsj	a4,rqdylp

	PULL	a2,a3,a4,a13
	rets


	.asg	CZMID,Z
rqdyseek_t
	.word	-280,Z-160, -200,Z-160,	-100,Z-160, -50,Z-160
	.word	0,Z
	.word	-280,Z+160, -200,Z+160,	-100,Z+160, -50,Z+160


rqdymdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	50,50,45,45,40		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15



 SUBRP	drone_chaseball

	move	*a13(plyr_rcvpass),a14
	jrgt	drone_offwoball		;I'm rcving ball?

	move	*a13(plyr_tmproc_p),a2,L
	move	*a2(plyr_rcvpass),a14
	jrgt	drone_offwoball		;Teammate rcving ball?


	clr	a0
	move	a0,*a13(plyr_d_cflgs)

	move	*a13(plyr_d_mode),a14
	jrz	kxhcinmd			;Already in mode?

	move	a0,*a13(plyr_d_mode)

	move	*a13(plyr_ptsdown),a14
	move	*a13(plyr_d_skill),a0
	add	a0,a14
	cmpi	15,a14
	jrle	kxhcmxdnok
	movk	15,a14
kxhcmxdnok	addk	15,a14
	jrge	kxhcdnok
	clr	a14
kxhcdnok	sll	4,a14
	addi	kxhcmdsk_t,a14
	move	*a14,a0
	callr	rndrng0
	addk	2,a0
	move	a0,*a13(plyr_d_seekcnt)

kxhcinmd

	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	kxhcseek

	move	*a2(plyr_seq),a0
	subi	STAGGER_SEQ,a0
	jrls	kxhctmok
	subk	FLYBACKWB2_SEQ-STAGGER_SEQ,a0
	jrls	kxhcchaseb			;Teammates staggered?
kxhctmok
	move	*a13(plyr_balldist),a0
	move	*a2(plyr_balldist),a14
	cmp	a14,a0
	jrlt	kxhcchaseb			;I'm closer?

	move	*a13(plyr_ohoopx),a0	;Go for opponents top of 3 pt line
	subi	WRLDMID,a0
	sra	2,a0			;/4
	addi	WRLDMID,a0
	movi	CZMID,a1
	jruc	kxhcsetxz

kxhcchaseb
	move	@ballobj_p,a5,L

	move	*a5(OXPOS),a0
	move	*a5(OXANI+16),a14
	add	a14,a0
	move	*a5(OXVEL),a14,L
	sra	16-3,a14		;8 ticks from now
	add	a14,a0

	move	*a5(OZPOS),a1
	move	*a5(OZVEL),a14,L
	sra	16-3,a14		;8 ticks from now
	add	a14,a1
kxhcsetxz
	move	a0,*a13(plyr_d_seekx)
	move	a1,*a13(plyr_d_seeky)


	move	*a13(plyr_ptsdown),a14
	move	*a13(plyr_d_skill),a0
	add	a0,a14
	cmpi	15,a14
	jrle	kxhcmxdnok2
	movk	15,a14
kxhcmxdnok2
	addk	15,a14
	jrge	kxhcdnok2
	clr	a14
kxhcdnok2	sll	4,a14
	addi	kxhcskt_t,a14
	move	*a14,a0

	callr	rndrng0
	addk	5,a0
kxhcseek	move	a0,*a13(plyr_d_seekcnt)

	callr	drone_seek


	move	*a13(plyr_balldist),a2	;0CHhk if I can jump at ball
	subk	30,a2
	jrge	kxhcnojmp

	move	*a5(OYPOS),a0
	addk	25,a0
	move	*a8(OYPOS),a1
	cmp	a1,a0
	jrlt	kxhcnojmp

	move	*a13(plyr_num),a0
	move	@plyrnumonfire,a1
	cmp	a0,a1
	jreq	kxhconfire			;On fire?

	movk	7,a0
	callr	rnd
	jrnz	kxhcnojmp			;87%?
kxhconfire
	move	@game_time,a1,L		;0DHon't take our own shots off the rim
	cmpi	0300H,a1
	jrgt	kxhcj			;More than 3 secs?

	move	*a13(plyr_num),a0
	move	@ballpnumshot,a1
	srl	1,a0
	srl	1,a1
	cmp	a0,a1
	jreq	kxhcnojmp			;My team?

kxhcj	move	*a11,a0			;Jmp
	ORK	BUT1_M,a14
	move	a0,*a11
kxhcnojmp

	rets

kxhcmdsk_t					;Mode switch max seek time
	.word	80,80,80,80,80		;Up 15-11
	.word	80,70,60,55,50		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

kxhcskt_t					;Max seek time
	.word	50,50,45,45,45
	.word	40,40,30,30,20
	.word	20,16,15,15,15
	.word	15
	.word	15,15,15,15,15
	.word	15,15,15,15,15
	.word	15,15,15,15,15



 SUBRP	drone_seek

	move	*a13(plyr_d_seekx),a0
	move	*a13(plyr_d_seeky),a1


 SUBRP	drone_seekxy

	move	a2,b0

	move	*a8(OXPOS),a2
	move	*a8(OXANI+16),a14
	add	a14,a2
	sub	a0,a2

	clr	a0

	move	a2,a14
	abs	a2
	subk	10,a2
	jrle	ntdaonx
	move	a14,a14
	jrlt	ntdanolft
	subk	4,a0			;Left

ntdanolft	addk	8,a0			;Rgt
ntdaonx
	move	*a8(OZPOS),a2

	sub	a1,a2
	move	a2,a14
	abs	a2
	subk	10,a2
	jrle	ntdaonz
	move	a14,a14
	jrlt	ntdanoup
	subk	1,a0			;Up

ntdanoup	addk	2,a0			;Dn
ntdaonz
	move	a0,*a11

	move	b0,a2
	move	a0,a0
	rets



 SUBRP	drone_getcurskillo

	move	*a13(plyr_ptsdown),a0
	move	*a13(plyr_d_skill),a14
	add	a14,a0
	cmpi	15,a0
	jrle	lrsumxdnok
	movk	15,a0
lrsumxdnok	addk	15,a0
	jrge	lrsudnok
	clr	a0
lrsudnok	sll	4,a0			;A7=Ptsdn+skill for indexing (*16)

	rets




 SUBR	drone2_adjskill

	PUSH	a2,a3,a4,a5,a6

	move	a0,a5
	subk	2,a5
	abs	a5
	move	@gmqrtr,a1
	cmpi	3,a1
	jrls	twroqok
	movk	3,a1			;Overtime
twroqok	movk	3,a0
	mpyu	a0,a1
	add	a1,a5			;A5=Quarter+minute index (0-11)


	movk	ADJDIFF,a0		;Get difficulty level
	calla	GET_ADJ			;Drone v2 is level 4 & 5
	subk	4,a0			;-2 to 2
	move	a0,a6
	sll	1,a0			;*2
	add	a0,a6			;A6=Difficulty adj (0 or 3)

	movi	plyrproc_t,a4
	movk	4,b0
twrolp
	move	*a4+,a3,L

	move	*a3(plyr_d_skill),a2

	move	*a3(plyr_ptsdown),a14
	cmpi	15,a14
	jrle	twromxdnok
	movk	15,a14
twromxdnok	addk	15,a14
	jrge	twrodnok
	clr	a14
twrodnok	sll	4,a14
	addi	twroadj_t,a14
	move	*a14,a14
	add	a14,a2

	move	a5,a14			;0CHhk quarter minimum
	sll	3,a14
	addi	twromin_t,a14
	movb	*a14,a14
	add	a6,a14
	cmp	a14,a2
	jrge	twrominok
	move	a14,a2
twrominok
	move	*a3(plyr_num),a1
	XORK	1,a1
	move	@PSTATUS,a14
	btst	a1,a14
	jrnz	twrotminok			;Teammate is human?

	move	@team1,a1		;0CHhk team minimum
	cmpi	3,b0
	jrge	twrot1
	move	@team2,a1
twrot1	movk	12,a0
	mpyu	a0,a1

	add	a5,a1
	sll	3,a1			;*8
	addi	twrotdmin_t,a1
	movb	*a1,a14
	add	a6,a14
	cmp	a14,a2
	jrge	twrotminok
	move	a14,a2
twrotminok
	move	a2,*a3(plyr_d_skill)

	dsj	b0,twrolp

	PULL	a2,a3,a4,a5,a6
	rets


twroadj_t	.word	-5,-5,-5,-5,-5
	.word	-5,-5,-5,-4,-3
	.word	-2,-1,0,0,0
	.word	0
	.word	1,1,2,2,3
	.word	3,4,4,5,5
	.word	5,6,6,6,7

twromin_t	.byte	-15,-12,-10, -8,-7,-6, -5,-4,-4, -3,-2,-3

TMDIFF	.macro
	.byte	-10,-9,-8, -7,-6,-5, -4,-4,-3, -2,0,-2
	.endm
twrotdmin_t
	TMDIFF						;ATL
	TMDIFF						;BOST
	TMDIFF						;CHAR
	.byte	6,7,8, 9,10,11, 12,12,13, 14,15,14	;CHI (1)
	.byte	-6,-5,-5, -4,-3,-3, -2,-2,-2, -1,0,-1	;CLEV (10)
	TMDIFF						;DAL
	TMDIFF						;DEN
	.byte	-5,-4,-4, -3,-2,-2, -1,-1,-1, 0,1,0	;DET (9)
	TMDIFF						;GOLD
	.byte	-4,-3,-3, -2,-1,-1, 0,0,0, 1,2,1	;HOU (8)
	.byte	-3,-2,-2, -1,0,0, 1,1,1, 2,3,2		;IND (7)
	TMDIFF						;LAC
	.byte	-1,-1,0, 1,1,1, 2,2,2, 3,4,3		;LAL (6)
	TMDIFF						;MIAMI
	TMDIFF						;MILW
	TMDIFF						;MINN
	TMDIFF						;NJ
	.byte	1,1,2, 3,3,3, 4,4,4, 5,6,5		;NY (4)
	TMDIFF						;ORL
	TMDIFF						;PHIL
	.byte	4,4,5, 6,6,6, 7,7,7, 8,9,8		;PHX (2)
	.byte	2,2,3, 4,4,4, 5,5,5, 6,7,6		;PORT (3)
	TMDIFF						;SAC
	TMDIFF						;SANANT
	TMDIFF						;SEA
	.byte	0,0,1, 2,2,2, 3,3,3, 4,5,4		;UTAH (5)
	TMDIFF						;WASH
	.even



 SUBRP	rnd

	move	@RAND,a1,L
	rl	a1,a1
	move	@HCOUNT,a14
	rl	a14,a1
	add	sp,a1
	move	a1,@RAND,L

	and	a1,a0
	rets



 SUBRP	rndrng0

	move	@RAND,a1,L
	rl	a1,a1
	move	@HCOUNT,a14
	rl	a14,a1
	add	sp,a1
	move	a1,@RAND,L

	addk	1,a0
	mpyu	a1,a0		;Condition codes not valid!

	rets




	.end

